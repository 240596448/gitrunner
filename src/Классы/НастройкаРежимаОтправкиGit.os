Перем мТекущаяНастройкаОтправки;
Перем мОтправкаRefspec;
Перем мПараметрыКомандыОтправки;
Перем мURLРепозитория;
Перем мЗаголовкиОтправки;
Перем Лог;

Функция ПолучитьНастройки() Экспорт
	
	мТекущаяНастройкаОтправки = Новый Структура;
	мТекущаяНастройкаОтправки.Добавить("URLРепозитория",мURLРепозитория);
	мТекущаяНастройкаОтправки.Добавить("ЗаголовкиОтправки",мЗаголовкиОтправки);
	мТекущаяНастройкаОтправки.Добавить("ПараметрыКомандыОтправки",мПараметрыКомандыОтправки);

	Возврат мТекущаяНастройкаОтправки;
	
КонецФункции

Процедура УстановитьНастройки(Знач НовыеНастройкиОтправки) Экспорт
	
	мURLРепозитория = НовыеНастройкиОтправки.Получить("URLРепозитория");
	мЗаголовкиОтправки = НовыеНастройкиОтправки.Получить("ЗаголовкиОтправки");
	мПараметрыКомандыОтправки = НовыеНастройкиОтправки.Получить("ПараметрыКомандыОтправки");

КонецПроцедуры

Функция ПолучитьПараметрыКоманды() Экспорт
	
	МассивПараметров = Новый Массив;
		
	Для каждого Настройка Из мПараметрыКомандыОтправки Цикл
		
		УстановитьПараметрКоманды(МассивПараметров, Настройка.Ключ, Настройка.Значение);
		
	КонецЦикла;

	Если ЗначениеЗаполнено(мURLРепозитория) Тогда
		МассивПараметров.Добавить(мURLРепозитория);
	КонецЕслИ;

	Для Каждого Заголовок Из мЗаголовкиОтправки Цикл
		МассивПараметров.Добавить(Заголовок)
	КонецЦикла;
	
	УдалитьВзаимоисключающиеПараметры(МассивПараметров);
	
	МассивПараметров.Вставить(0, "push");
	
	Возврат МассивПараметров;
	
КонецФункции // ПолучитьНастройкиКоманды() Экспорт

Процедура ДобавитьПроизвольныйЗаголовокОтправки(Знач ЗаголовокОтправки) Экспорт
	
	ПроверитьЗаголовкиНаRefspec(ЗаголовокОтправки);
	
	мЗаголовкиОтправки.Добавить(ЗаголовокОтправки);

КонецПроцедуры

Процедура УстановитьURLРепозиторияОтправки(Знач URLРепозитория) Экспорт

	мURLРепозитория = URLРепозитория;
	
КонецПроцедуры // УстановитьРепозиторийОтправки()

Процедура УстановитьЗаголовкиОтправки(Знач ЗаголовкиОтправки) Экспорт
	
	
	Если Не ТипЗнч(ЗаголовкиОтправки) = Тип("Массив") Тогда
		
		ПроверитьЗаголовкиНаRefspec(ЗаголовкиОтправки);
		мЗаголовкиОтправки = Новый Массив;
	
		мЗаголовкиОтправки.Добавить(ЗаголовкиОтправки); 
	Иначе

		мЗаголовкиОтправки = ЗаголовкиОтправки;

	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПараметрыКомандыОтправки(Знач Параметры) Экспорт
	
	мПараметрыКомандыОтправки = Параметры;

КонецПроцедуры


Процедура ДобавитьЗаголовкиОтправки(ЗаголовокРабочейКопии = "", ЗаголовокУдаленногоСервера = "") Экспорт
	
	НовыйЗаголовокОтправки = "";
	
	Если НЕ ЗначениеЗаполнено(ЗаголовокУдаленногоСервера) Тогда
		
		НовыйЗаголовокОтправки = СтрШаблон("%1", ЗаголовокРабочейКопии);
		
	Иначе
		
		НовыйЗаголовокОтправки = СтрШаблон("%1:%2",ЗаголовокРабочейКопии, ЗаголовокУдаленногоСервера);
		
	КонецЕслИ;
	
	ДобавитьПроизвольныйЗаголовокОтправки(НовыйЗаголовокОтправки);
	
КонецПроцедуры

Процедура ОтображатьПрогресс(Знач ЗначениеУстановки = Истина) Экспорт
	
	УстановитьПроизвольныйРежим("--progress", ЗначениеУстановки);
	
КонецПроцедуры

Процедура РежимТишины(Знач ЗначениеУстановки = Истина) Экспорт
	
	УстановитьПроизвольныйРежим("--quiet", ЗначениеУстановки);
	
КонецПроцедуры

Процедура РежимЗеркала(Знач ЗначениеУстановки = Истина) Экспорт
	
	УстановитьПроизвольныйРежим("--mirror", ЗначениеУстановки);
	
КонецПроцедуры 

Процедура РежимУдаления(Знач ЗначениеУстановки = Истина) Экспорт
	
	УстановитьПроизвольныйРежим("--delete", ЗначениеУстановки);
	
КонецПроцедуры

Процедура ПолнаяОтправка(Знач ЗначениеУстановки = Истина) Экспорт
	
	УстановитьПроизвольныйРежим("--all", ЗначениеУстановки);
	
КонецПроцедуры

Процедура ПерезаписатьИсторию(Знач ЗначениеУстановки = Истина) Экспорт
	
	УстановитьПроизвольныйРежим("--force", ЗначениеУстановки);
	
КонецПроцедуры

Процедура Отслеживать(Знач ЗначениеУстановки = Истина) Экспорт
	
	УстановитьПроизвольныйРежим("--set-upstream", ЗначениеУстановки);
	
КонецПроцедуры

Процедура РежимПрограммнойОбработкаРезультата(Знач ЗначениеУстановки = Истина) Экспорт
	
	УстановитьПроизвольныйРежим("--porcelain", ЗначениеУстановки);
	
КонецПроцедуры

Процедура ОтправкаТэгов(Знач ЗначениеУстановки = Истина) Экспорт
	
	УстановитьПроизвольныйРежим("--tags", ЗначениеУстановки);
	
КонецПроцедуры

Процедура УстановитьПроизвольныйРежим(Знач НаименованиеПараметра, Знач ЗначениеУстановки = Истина) Экспорт
		
	мПараметрыКомандыОтправки.Вставить(НаименованиеПараметра, ЗначениеУстановки);

КонецПроцедуры

Процедура ЗаполнитьНастройкиПоУмолчанию()
	
	мПараметрыКомандыОтправки = Новый Соответствие;
	Отключить = Ложь;
	
	РежимПрограммнойОбработкаРезультата();
	РежимУдаления(Отключить);
	РежимЗеркала(Отключить);
	РежимТишины(Отключить);
	
	Отслеживать(Отключить);
	ОтправкаТэгов(Отключить);
	ПерезаписатьИсторию(Отключить);
	ПолнаяОтправка(Отключить);
	ОтображатьПрогресс(Отключить);
	
	мURLРепозитория = "";
	мЗаголовкиОтправки = Новый Массив;
	
	мОтправкаRefspec = Ложь;
	
КонецПроцедуры

Процедура ПроверитьЗаголовкиНаRefspec(Знач ЗаголовокОтправки)
	
	Если мОтправкаRefspec Тогда
		Возврат;
	КонецЕсли;	
	
	Если СтрНайти("refs/", ЗаголовокОтправки) > 0 Тогда
		
		мОтправкаRefspec = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПараметрКоманды(МассивПараметров, Знач КлючПараметра, Знач НадоУстановить)
	
	Если Не НадоУстановить Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрСовместимСRefspec(КлючПараметра) Тогда
		МассивПараметров.Добавить(КлючПараметра);
	КонецЕсли;	
	
КонецПроцедуры

Процедура УдалитьВзаимоисключающиеПараметры(МассивПараметров)
	
	МассивПроверки = МассивВзаимоисключающихПараметров();
	
	ЭлементПроверки = Неопределено;
	
	Для каждого Параметр Из МассивПараметров Цикл
		
		ИндексЭлементаПроверки = МассивПроверки.Найти(Параметр);
		
		Если НЕ ИндексЭлементаПроверки = Неопределено Тогда
			ЭлементПроверки = МассивПроверки.Получить(ИндексЭлементаПроверки);
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ЭлементПроверки = Неопределено Тогда
		
		Для каждого ПараметрДляУдаления Из МассивПроверки Цикл
			
			Если ЭлементПроверки = ПараметрДляУдаления Тогда
				Продолжить;
			КонецЕсли;
			
			ИндексЭлементаУдаления = МассивПараметров.Найти(ПараметрДляУдаления);
			
			Если НЕ ИндексЭлементаУдаления = Неопределено Тогда
				МассивПараметров.Удалить(ИндексЭлементаУдаления);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕслИ;

КонецПроцедуры

Функция ПараметрСовместимСRefspec(Знач КлючПараметра)
	
	Если Не мОтправкаRefspec Тогда
		Возврат Истина;
	КонецЕсли;
	
	МассивПроверки = МассивНесовместимыхПараметровСRefspec();
	Если ЗначениеЗаполнено(МассивПроверки.Найти(КлючПараметра)) Тогда
		Возврат Ложь;
	КонецЕслИ;
	
КонецФункции // ПроверитьСовместимостьПараметра()

Функция МассивНесовместимыхПараметровСRefspec()
	
	ФиксированныйМассив = Новый Массив;
	ФиксированныйМассив.Добавить("--all");
	ФиксированныйМассив.Добавить("--mirror");
	ФиксированныйМассив.Добавить("--tags");
	
	Возврат ФиксированныйМассив;
	
КонецФункции

Функция МассивВзаимоисключающихПараметров()
	
	ФиксированныйМассив = Новый Массив;
	ФиксированныйМассив.Добавить("--all");
	ФиксированныйМассив.Добавить("--mirror");
	ФиксированныйМассив.Добавить("--tags");
	ФиксированныйМассив.Добавить("--delete");
	
	Возврат ФиксированныйМассив;

КонецФункции

Процедура Инициализация()
	
	Лог = Логирование.ПолучитьЛог("oscript.lib.gitrunner");
    ЗаполнитьНастройкиПоУмолчанию();
	
КонецПроцедуры

Инициализация();


