Перем мТекущаяНастройкаОтправки;
Перем мОтправкаRefspec;
Перем мГитРепозиторий;
Перем Лог;

Функция ПолучитьНастройки() Экспорт
	
	Возврат мТекущаяНастройкаОтправки;
	
КонецФункции

Процедура УстановитьНастройки(Знач НовыеНастройкиОтправки) Экспорт
	
	мТекущаяНастройкаОтправки = НовыеНастройкиОтправки;
	
КонецПроцедуры

Функция ПолучитьПараметрыКоманды() Экспорт
	
	МассивПараметров = Новый Массив;
	
	Для каждого Настройка Из мТекущаяНастройкаОтправки Цикл
		
		Если Настройка.Ключ = "ЗаголовкиОтправки" Тогда
			
			Продолжить
			
		Иначе
			
			УстановитьПараметрКоманды(МассивПараметров, Настройка.Ключ, Настройка.Значение);
			
		КонецЕсли;
		
	КонецЦикла;

	ЗаголовкиОтправки = мТекущаяНастройкаОтправки.Получить("ЗаголовкиОтправки");
	Для Каждого Заголовок Из ЗаголовкиОтправки Цикл
		МассивПараметров.Добавить(Заголовок)
	КонецЦикла;
	
	УдалитьВзаимоисключающиеПараметры(МассивПараметров);
	
	МассивПараметров.Вставить(0, "push");

	Возврат МассивПараметров;
	
КонецФункции // ПолучитьНастройкиКоманды() Экспорт

Процедура ДобавитьПроизвольныйЗаголовокОтправки(Знач ЗаголовокОтправки) Экспорт
	
	ВставитьПроизвольныйЗаголовокОтправки(ЗаголовокОтправки);
	
КонецПроцедуры

Процедура УстановитьРепозиторийОтправки(Знач ИмяРепозитория) Экспорт

	ВставитьПроизвольныйЗаголовокОтправки(ИмяРепозитория, 0);
	
КонецПроцедуры // УстановитьРепозиторийОтправки()

Процедура УстановитьВеткуОтправки(Знач ИмяВетки) Экспорт
	
	Если мОтправкаRefspec Тогда
		Лог.Информация("Установка ветки для отправки несовместима с режимом отправки refspec");
		Возврат;
	КонецЕсли;

	ВставитьПроизвольныйЗаголовокОтправки(ИмяВетки, 1);
		
КонецПроцедуры

Процедура ВставитьПроизвольныйЗаголовокОтправки(Знач ЗаголовокОтправки, Знач ИндексМассива = Неопределено)
	
	ТекущиеЗаголовкиОтправки = мТекущаяНастройкаОтправки.Получить("ЗаголовкиОтправки");
	
	ПроверитьЗаголовкиНаRefspec(ЗаголовокОтправки);
	
	Если ИндексМассива = Неопределено Тогда
		ТекущиеЗаголовкиОтправки.Добавить(ЗаголовокОтправки);
	Иначе
		ТекущиеЗаголовкиОтправки.Вставить(ИндексМассива, ЗаголовокОтправки);
	КонецЕсли;
	
	мТекущаяНастройкаОтправки.Вставить("ЗаголовкиОтправки", ТекущиеЗаголовкиОтправки);
	
КонецПроцедуры


Процедура ДобавитьЗаголовкиОтправки(ЗаголовокРабочейКопии = "", ЗаголовокУдаленногоСервера = "") Экспорт
	
	НовыйЗаголовокОтправки = "";
	
	Если НЕ ЗначениеЗаполнено(ЗаголовокУдаленногоСервера) Тогда
		
		НовыйЗаголовокОтправки = СтрШаблон("%1", ЗаголовокРабочейКопии);
		
	Иначе
		
		НовыйЗаголовокОтправки = СтрШаблон("%1:%2",ЗаголовокРабочейКопии, ЗаголовокУдаленногоСервера);
		
	КонецЕслИ;
	
	ДобавитьПроизвольныйЗаголовокОтправки(НовыйЗаголовокОтправки);
	
КонецПроцедуры

Процедура ОтображатьПрогресс(Знач ЗначениеУстановки = Истина) Экспорт
	
	УстановитьПроизвольныйРежим("--progress", ЗначениеУстановки);
	
КонецПроцедуры

Процедура РежимТишины(Знач ЗначениеУстановки = Истина) Экспорт
	
	УстановитьПроизвольныйРежим("--quiet", ЗначениеУстановки);
	
КонецПроцедуры

Процедура РежимЗеркала(Знач ЗначениеУстановки = Истина) Экспорт
	
	УстановитьПроизвольныйРежим("--mirror", ЗначениеУстановки);
	
КонецПроцедуры 

Процедура РежимУдаления(Знач ЗначениеУстановки = Истина) Экспорт
	
	УстановитьПроизвольныйРежим("--delete", ЗначениеУстановки);
	
КонецПроцедуры

Процедура ПолнаяОтправка(Знач ЗначениеУстановки = Истина) Экспорт
	
	УстановитьПроизвольныйРежим("--all", ЗначениеУстановки);
	
КонецПроцедуры

Процедура ПерезаписатьИсторию(Знач ЗначениеУстановки = Истина) Экспорт
	
	УстановитьПроизвольныйРежим("--force", ЗначениеУстановки);
	
КонецПроцедуры

Процедура Отслеживать(Знач ЗначениеУстановки = Истина) Экспорт
	
	УстановитьПроизвольныйРежим("--set-upstream", ЗначениеУстановки);
	
КонецПроцедуры

Процедура РежимПрограммнойОбработкаРезультата(Знач ЗначениеУстановки = Истина) Экспорт
	
	УстановитьПроизвольныйРежим("--porcelain", ЗначениеУстановки);
	
КонецПроцедуры

Процедура ОтправкаТэгов(Знач ЗначениеУстановки = Истина) Экспорт
	
	УстановитьПроизвольныйРежим("--tags", ЗначениеУстановки);
	
КонецПроцедуры

Процедура УстановитьПроизвольныйРежим(Знач НаименованиеПараметра, Знач ЗначениеУстановки = Истина) Экспорт
	
	мТекущаяНастройкаОтправки.Вставить(НаименованиеПараметра, ЗначениеУстановки);
	
КонецПроцедуры

Процедура ЗаполнитьНастройкиПоУмолчанию()
	
	мТекущаяНастройкаОтправки.Вставить("ЗаголовкиОтправки", Новый Массив);
	мТекущаяНастройкаОтправки.Вставить("--tags", Ложь);
	мТекущаяНастройкаОтправки.Вставить("--porcelain", Истина);
	мТекущаяНастройкаОтправки.Вставить("--delete", Ложь);
	мТекущаяНастройкаОтправки.Вставить("--progress", Ложь);
	мТекущаяНастройкаОтправки.Вставить("--quiet", Ложь);
	мТекущаяНастройкаОтправки.Вставить("--all", Ложь);
	мТекущаяНастройкаОтправки.Вставить("--force", Ложь);
	
	мОтправкаRefspec = Ложь;
	
КонецПроцедуры

Процедура ПроверитьЗаголовкиНаRefspec(Знач ЗаголовокОтправки)
	
	Если мОтправкаRefspec Тогда
		Возврат;
	КонецЕсли;	
	
	Если СтрНайти("refs/", ЗаголовокОтправки) > 0 Тогда
		
		мОтправкаRefspec = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПараметрКоманды(МассивПараметров, Знач КлючПараметра, Знач НадоУстановить)
	
	Если Не НадоУстановить Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрСовместимСRefspec(КлючПараметра) Тогда
		МассивПараметров.Добавить(КлючПараметра);
	КонецЕсли;	
	
КонецПроцедуры

Процедура УдалитьВзаимоисключающиеПараметры(МассивПараметров)
	
	МассивПроверки = МассивВзаимоисключающихПараметров();
	
	ЭлементПроверки = Неопределено;
	
	Для каждого Параметр Из МассивПараметров Цикл
		
		ИндексЭлементаПроверки = МассивПроверки.Найти(Параметр);
		
		Если НЕ ИндексЭлементаПроверки = Неопределено Тогда
			ЭлементПроверки = МассивПроверки.Получить(ИндексЭлементаПроверки);
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ЭлементПроверки = Неопределено Тогда
		
		Для каждого ПараметрДляУдаления Из МассивПроверки Цикл
			
			Если ЭлементПроверки = ПараметрДляУдаления Тогда
				Продолжить;
			КонецЕсли;
			
			ИндексЭлементаУдаления = МассивПараметров.Найти(ПараметрДляУдаления);
			
			Если НЕ ИндексЭлементаУдаления = Неопределено Тогда
				МассивПараметров.Удалить(ИндексЭлементаУдаления);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕслИ;

КонецПроцедуры

Функция ПараметрСовместимСRefspec(Знач КлючПараметра)
	
	Если Не мОтправкаRefspec Тогда
		Возврат Истина;
	КонецЕсли;
	
	МассивПроверки = МассивНесовместимыхПараметровСRefspec();
	Если ЗначениеЗаполнено(МассивПроверки.Найти(КлючПараметра)) Тогда
		Возврат Ложь;
	КонецЕслИ;
	
КонецФункции // ПроверитьСовместимостьПараметра()

Функция МассивНесовместимыхПараметровСRefspec()
	
	ФиксированныйМассив = Новый Массив;
	ФиксированныйМассив.Добавить("--all");
	ФиксированныйМассив.Добавить("--mirror");
	ФиксированныйМассив.Добавить("--tags");
	
	Возврат ФиксированныйМассив;
	
КонецФункции

Функция МассивВзаимоисключающихПараметров()
	
	ФиксированныйМассив = Новый Массив;
	ФиксированныйМассив.Добавить("--all");
	ФиксированныйМассив.Добавить("--mirror");
	ФиксированныйМассив.Добавить("--tags");
	
	Возврат ФиксированныйМассив;

КонецФункции

Процедура Инициализация()
	
	Лог = Логирование.ПолучитьЛог("oscript.lib.gitrunner");
    
    мТекущаяНастройкаОтправки = Новый Соответствие;
	ЗаполнитьНастройкиПоУмолчанию();
	
КонецПроцедуры

Инициализация();


